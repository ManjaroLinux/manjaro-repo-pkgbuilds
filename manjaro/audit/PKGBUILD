# Contributors:
#	Connor Behan <connor.behan@gmail.com>
#	henning mueller <henning@orgizm.net>

pkgname=audit
pkgver=2.1.1
pkgrel=2
pkgdesc='User space utilities for storing and searching the audit records generated by the audit subsystem in the Linux 2.6 kernel.'
url=http://people.redhat.com/sgrubb/$pkgname
arch=(i686 x86_64)
depends=('kernel26-headers>=2.6.30' python2 libldap)
makedepends=(libldap swig)
license=(GPL)
options=(!libtool)
backup=(
	etc/libaudit.conf
	etc/audit/audit.rules
	etc/audit/auditd.conf
	etc/audisp/audispd.conf
	etc/audisp/audisp-remote.conf
	etc/audisp/zos-remote.conf
	etc/audisp/plugins.d/af_unix.conf
	etc/audisp/plugins.d/audispd-zos-remote.conf
	etc/audisp/plugins.d/au-remote.conf
	etc/audisp/plugins.d/syslog.conf
)
source=(
	$url/$pkgname-$pkgver.tar.gz
	auditd.rc
	python2.patch
)
md5sums=(
	24c15eb02a100b0fcf809a29b32f0dbd
	bd5574064e0c88748aebf0cd92ebcf0c
	45a33c6bef84f8e1701cb0b5e187336b
)
	 
build() {
	cd $srcdir/$pkgname-$pkgver
	patch -p0 < ../python2.patch
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--libexecdir=/usr/lib/audit \
		--with-apparmor
	make
}

package() {
	cd $srcdir/$pkgname-$pkgver
	make DESTDIR=$pkgdir install

	cd $pkgdir
	install -m744 $srcdir/auditd.rc etc/rc.d/auditd
	install -d var/log/audit
	rm -rf \
		etc/rc.d/init.d \
		etc/sysconfig
	mv usr/sbin sbin
}
