# $Id: PKGBUILD 127034 2011-06-09 22:34:09Z tomegun $
# Maintainer: Tom Gundersen <teg@jklm.no>
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>
# Maintainer: Aaron Griffin <aaron@archlinux.org>

pkgname=initscripts-manjaro
provides=('initscripts=2011.06.4')
conflicts=('initscripts')
pkgver=2011.06.4
pkgrel=1
pkgdesc="System initialization/bootup scripts"
arch=('i686' 'x86_64')
url="http://www.archlinux.org"
license=('GPL2')
backup=(etc/inittab etc/rc.conf etc/rc.local etc/rc.local.shutdown etc/conf.d/wireless)
depends=('glibc' 'bash' 'grep' 'coreutils' 'udev>=171' 'iproute2'
         'ncurses' 'kbd' 'findutils' 'sysvinit')
optdepends=('bridge-utils: Network bridging support'
            'dhcpcd: DHCP network configuration'
            'net-tools: legacy network support'
            'wireless_tools: Wireless networking')
install=initscripts.install
source=("ftp://ftp.archlinux.org/other/initscripts/initscripts-${pkgver}.tar.xz"
        'wireless.conf.d')

package() {
  cd ${srcdir}/initscripts-${pkgver}
  make DESTDIR=${pkgdir} install
  install -D -m644 ${srcdir}/wireless.conf.d ${pkgdir}/etc/conf.d/wireless

	sed -i 's|Arch Linux|Manjaro Linux|g' "${pkgdir}/etc/rc.sysinit"
	sed -i 's|www.archlinux.org|www.manjarolinux.org|g' "${pkgdir}/etc/rc.sysinit"
	sed -i 's|C_OTHER=.*|C_OTHER=\"\\033[1\;32\;40m\"|g' "${pkgdir}/etc/rc.d/functions"
	sed -i 's|C_BUSY=.*|C_OTHER=\"\\033[0\;32\;40m\"|g' "${pkgdir}/etc/rc.d/functions"
	sed -i 's|C_H2=.*|C_H2=\"\\033[1\;32\;40m\"|g' "${pkgdir}/etc/rc.d/functions"
}

md5sums=('34cbe55f3f42069c59237db935d0511b'
         '027576534885b8d5dded9be546057b12')
