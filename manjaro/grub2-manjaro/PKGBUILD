# Maintainer: Guinux <nuxgui@gmail.com>

# _grub2_rev=3238

_grub2_lua_ver=20
_grub2_gpxe_ver=12
_grub2_ntldr_ver=17
_grub2_915_ver=7

pkgname=grub2-manjaro
pkgver=1.99
pkgrel=1
pkgdesc="The GNU GRand Unified Bootloader version 2 - Built for PC BIOS"
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.gnu.org/software/grub/"
install="grub2-manjaro.install"
depends=('xz' 'freetype2' 'device-mapper' 'gettext' 'texinfo' 'os-prober')
makedepends=('bdf-unifont' 'python2' 'xz' 'autogen' 'texinfo' 'help2man' 'gettext' 'device-mapper')
conflicts=('grub' 'grub2-common' 'grub2-bios')
provides=('grub2-common=1:1.99' 'grub2-bios=1:1.99')
backup=('boot/grub/grub.cfg' 'etc/default/grub' 'etc/grub.d/40_custom')
epoch=1
options=(strip purge docs zipman !emptydirs)

source=("ftp://ftp.gnu.org/gnu/grub/grub-1.99.tar.xz"
	"ftp://ftp.archlinux.org/other/grub2/grub2_extras_lua_r${_grub2_lua_ver}.tar.xz"
	"ftp://ftp.archlinux.org/other/grub2/grub2_extras_gpxe_r${_grub2_gpxe_ver}.tar.xz"
	"ftp://ftp.archlinux.org/other/grub2/grub2_extras_ntldr-img_r${_grub2_ntldr_ver}.tar.xz"
	"ftp://ftp.archlinux.org/other/grub2/grub2_extras_915resolution_r${_grub2_915_ver}.tar.xz"
	'manjaro_grub2_mkconfig_fixes.patch'
	'grub.default'
	'grub.cfg'
	'20_memtest86+')

noextract=("grub2_extras_lua_r${_grub2_lua_ver}.tar.xz"
	"grub2_extras_gpxe_r${_grub2_gpxe_ver}.tar.xz"
	"grub2_extras_ntldr-img_r${_grub2_ntldr_ver}.tar.xz"
	"grub2_extras_915resolution_r${_grub2_915_ver}.tar.xz")

build() {
	# set architecture dependent variables
	if [ "${CARCH}" = "x86_64" ] ; then
		_EFIEMU="--enable-efiemu"
	else
		_EFIEMU="--disable-efiemu"
	fi
	_HOST="${CARCH}"

	# copy the source for building the common/bios package
	cp -r ${srcdir}/grub-${pkgver} ${srcdir}/grub2_bios-${pkgver}
  
	## Apply Archlinux specific fixes to enable grub2-mkconfig detect Arch kernels and initramfs
	cd ${srcdir}/grub2_bios-${pkgver}
	patch -Np1 -i ${srcdir}/manjaro_grub2_mkconfig_fixes.patch

	# add the grub-extra sources
	export GRUB_CONTRIB=${srcdir}/grub2_bios-${pkgver}/grub2-extras/  
	install -d ${srcdir}/grub2_bios-${pkgver}/grub2-extras 
	bsdtar xf ${srcdir}/grub2_extras_lua_r${_grub2_lua_ver}.tar.xz \
		-C ${srcdir}/grub2_bios-${pkgver}/grub2-extras
	bsdtar xf ${srcdir}/grub2_extras_gpxe_r${_grub2_gpxe_ver}.tar.xz \
		-C ${srcdir}/grub2_bios-${pkgver}/grub2-extras
	bsdtar xf ${srcdir}/grub2_extras_ntldr-img_r${_grub2_ntldr_ver}.tar.xz \
		-C ${srcdir}/grub2_bios-${pkgver}/grub2-extras
	bsdtar xf ${srcdir}/grub2_extras_915resolution_r${_grub2_915_ver}.tar.xz \
		-C ${srcdir}/grub2_bios-${pkgver}/grub2-extras
  
	## Need to use python2
	sed -i 's|python|python2|' ${srcdir}/grub2_bios-${pkgver}/autogen.sh

	# start the actual build process
	cd ${srcdir}/grub2_bios-${pkgver}
	./autogen.sh
  
	## fix unifont.bdf location so grub-mkfont can create *.pf2 files
	sed -i 's|/usr/share/fonts/unifont|/usr/share/fonts/misc|' ${srcdir}/grub2_bios-${pkgver}/configure
	
		CFLAGS="" ./configure \
		--with-platform=pc --enable-mm-debug \
		${_EFIEMU} --host=${CARCH}-unknown-linux-gnu \
		--enable-grub-mkfont --prefix=/usr \
		--bindir=/bin --sbindir=/sbin \
		--mandir=/usr/share/man --infodir=/usr/share/info \
		--sysconfdir=/etc --enable-nls \
		--program-transform-name=s,grub,grub,
	CFLAGS="" make 
}

package() {
	cd ${srcdir}/grub2_bios-${pkgver}
	make DESTDIR=${pkgdir} install
  
	install -Dm755 ${pkgdir}/sbin/grub-install ${pkgdir}/sbin/grub_bios-install
	install -Dm755 ${pkgdir}/sbin/grub-install ${pkgdir}/sbin/grub_efi_x86_64-install
	install -Dm755 ${pkgdir}/sbin/grub-install ${pkgdir}/sbin/grub_efi_i386-install
    
	sed -i "s|^\(target_cpu\)=.*|\1=i386|; \
		s|^\(platform\)=.*|\1=pc|" \
		${pkgdir}/sbin/grub_bios-install

	sed -i "s|^\(target_cpu\)=.*|\1=x86_64|; \
		s|^\(platform\)=.*|\1=efi|" \
		${pkgdir}/sbin/grub_efi_x86_64-install

	sed -i "s|^\(target_cpu\)=.*|\1=i386|; \
		s|^\(platform\)=.*|\1=efi|" \
		${pkgdir}/sbin/grub_efi_i386-install
  
	## install extra /etc/grub.d/ files
	install -Dm755 ${srcdir}/20_memtest86+ ${pkgdir}/etc/grub.d/20_memtest86+
    
	## install /etc/default/grub (used by grub-mkconfig)
	install -Dm644 ${srcdir}/grub.default ${pkgdir}/etc/default/grub
	
	## install grub.cfg (needed so it doesn't get removed on upgrading because it was previously here)
	install -Dm644 ${srcdir}/grub.cfg ${pkgdir}/boot/grub/grub.cfg
}

md5sums=('27e360b951f006c464299d06bbd63442'
         'cb2f35c9cae0650d84e83e416ab5074d'
         '9e0944f3e63586dc4242fd8b7977e3c4'
         '8b72e7937e5987926b9420b9188cc458'
         '25e602305482042084534e6d7bdb5b0c'
         '394a19ad47591cd582948c7c407f1261'
         'f9591ea4be83ae7a62c1168c14d7bdac'
         '2bbeb06ff5aa81b336c269d5cffad4a0'
         '5a07e04c4ecb8ed145d54fec3043e0d5')
